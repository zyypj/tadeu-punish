plugins {
    id 'java'
    id("com.github.johnrengelman.shadow") version "7.1.0"
}

group = 'com.github.zyypj'
version = '1.0'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly files('libs/spigot.jar')
    compileOnly("org.projectlombok:lombok:1.18.38")
    compileOnly files('libs/legendchat.jar')
    compileOnly files('libs/nChat.jar')
    compileOnly files('libs/ultimatechat.jar')

    annotationProcessor("org.projectlombok:lombok:1.18.38")

    implementation 'com.github.zyypj:tadeuBooter:1.7.1'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


shadowJar {
    relocate 'com.github.zyypj.tadeuBooter', 'com.github.zyypj.tadeu.punish.relocated.tadeuBooter'
    archiveClassifier.set('')
    archiveFileName.set('TadeuPunish.jar')
    minimize()
}